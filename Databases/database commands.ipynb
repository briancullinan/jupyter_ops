{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "mysql2sqlite.sh\n",
    "\n",
    "\n",
    "from https://gist.github.com/esperlu/943776#file-mysql2sqlite-sh\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#!/bin/sh\n",
    "\n",
    "# Converts a mysqldump file into a Sqlite 3 compatible file. It also extracts the MySQL `KEY xxxxx` from the\n",
    "# CREATE block and create them in separate commands _after_ all the INSERTs.\n",
    "\n",
    "# Awk is choosen because it's fast and portable. You can use gawk, original awk or even the lightning fast mawk.\n",
    "# The mysqldump file is traversed only once.\n",
    "\n",
    "# Usage: $ ./mysql2sqlite mysqldump-opts db-name | sqlite3 database.sqlite\n",
    "# Example: $ ./mysql2sqlite --no-data -u root -pMySecretPassWord myDbase | sqlite3 database.sqlite\n",
    "\n",
    "# Thanks to and @artemyk and @gkuenning for their nice tweaks.\n",
    "\n",
    "mysqldump  --compatible=ansi --skip-extended-insert --compact  \"$@\" | \\\n",
    "\n",
    "awk '\n",
    "\n",
    "BEGIN {\n",
    "\tFS=\",$\"\n",
    "\tprint \"PRAGMA synchronous = OFF;\"\n",
    "\tprint \"PRAGMA journal_mode = MEMORY;\"\n",
    "\tprint \"BEGIN TRANSACTION;\"\n",
    "}\n",
    "\n",
    "# CREATE TRIGGER statements have funny commenting.  Remember we are in trigger.\n",
    "/^\\/\\*.*CREATE.*TRIGGER/ {\n",
    "\tgsub( /^.*TRIGGER/, \"CREATE TRIGGER\" )\n",
    "\tprint\n",
    "\tinTrigger = 1\n",
    "\tnext\n",
    "}\n",
    "\n",
    "# The end of CREATE TRIGGER has a stray comment terminator\n",
    "/END \\*\\/;;/ { gsub( /\\*\\//, \"\" ); print; inTrigger = 0; next }\n",
    "\n",
    "# The rest of triggers just get passed through\n",
    "inTrigger != 0 { print; next }\n",
    "\n",
    "# Skip other comments\n",
    "/^\\/\\*/ { next }\n",
    "\n",
    "# Print all `INSERT` lines. The single quotes are protected by another single quote.\n",
    "/INSERT/ {\n",
    "\tgsub( /\\\\\\047/, \"\\047\\047\" )\n",
    "\tgsub(/\\\\n/, \"\\n\")\n",
    "\tgsub(/\\\\r/, \"\\r\")\n",
    "\tgsub(/\\\\\"/, \"\\\"\")\n",
    "\tgsub(/\\\\\\\\/, \"\\\\\")\n",
    "\tgsub(/\\\\\\032/, \"\\032\")\n",
    "\tprint\n",
    "\tnext\n",
    "}\n",
    "\n",
    "# Print the `CREATE` line as is and capture the table name.\n",
    "/^CREATE/ {\n",
    "\tprint\n",
    "\tif ( match( $0, /\\\"[^\\\"]+/ ) ) tableName = substr( $0, RSTART+1, RLENGTH-1 ) \n",
    "}\n",
    "\n",
    "# Replace `FULLTEXT KEY` or any other `XXXXX KEY` except PRIMARY by `KEY`\n",
    "/^  [^\"]+KEY/ && !/^  PRIMARY KEY/ { gsub( /.+KEY/, \"  KEY\" ) }\n",
    "\n",
    "# Get rid of field lengths in KEY lines\n",
    "/ KEY/ { gsub(/\\([0-9]+\\)/, \"\") }\n",
    "\n",
    "# Print all fields definition lines except the `KEY` lines.\n",
    "/^  / && !/^(  KEY|\\);)/ {\n",
    "\tgsub( /AUTO_INCREMENT|auto_increment/, \"\" )\n",
    "\tgsub( /(CHARACTER SET|character set) [^ ]+ /, \"\" )\n",
    "\tgsub( /DEFAULT CURRENT_TIMESTAMP ON UPDATE CURRENT_TIMESTAMP|default current_timestamp on update current_timestamp/, \"\" )\n",
    "\tgsub( /(COLLATE|collate) [^ ]+ /, \"\" )\n",
    "\tgsub(/(ENUM|enum)[^)]+\\)/, \"text \")\n",
    "\tgsub(/(SET|set)\\([^)]+\\)/, \"text \")\n",
    "\tgsub(/UNSIGNED|unsigned/, \"\")\n",
    "\tif (prev) print prev \",\"\n",
    "\tprev = $1\n",
    "}\n",
    "\n",
    "# `KEY` lines are extracted from the `CREATE` block and stored in array for later print \n",
    "# in a separate `CREATE KEY` command. The index name is prefixed by the table name to \n",
    "# avoid a sqlite error for duplicate index name.\n",
    "/^(  KEY|\\);)/ {\n",
    "\tif (prev) print prev\n",
    "\tprev=\"\"\n",
    "\tif ($0 == \");\"){\n",
    "\t\tprint\n",
    "\t} else {\n",
    "\t\tif ( match( $0, /\\\"[^\"]+/ ) ) indexName = substr( $0, RSTART+1, RLENGTH-1 ) \n",
    "\t\tif ( match( $0, /\\([^()]+/ ) ) indexKey = substr( $0, RSTART+1, RLENGTH-1 ) \n",
    "\t\tkey[tableName]=key[tableName] \"CREATE INDEX \\\"\" tableName \"_\" indexName \"\\\" ON \\\"\" tableName \"\\\" (\" indexKey \");\\n\"\n",
    "\t}\n",
    "}\n",
    "\n",
    "# Print all `KEY` creation lines.\n",
    "END {\n",
    "\tfor (table in key) printf key[table]\n",
    "\tprint \"END TRANSACTION;\"\n",
    "}\n",
    "'\n",
    "exit 0"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Bash",
   "language": "bash",
   "name": "bash"
  },
  "language_info": {
   "codemirror_mode": "shell",
   "file_extension": ".sh",
   "mimetype": "text/x-sh",
   "name": "bash"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
